sl-comments  : # Comment
             : $ Comment
             : £ Comment
             : € Comment
             : ¥ Comment

number       : INT|FLOAT

string       : (SINGLE-QUOTE OR DOUBLE-QUOTE) characters (SINGLE-QUOTE OR DOUBLE-QUOTE)

function_std : print(value) -> STDOUT
             : clear() / cls() -> STDOUT
             : run(STRING: FILEPATH) -> STDOUT + MEMORY
             : exit() -> MEMORY
             : append(LIST, value: item) -> LIST
             : pop(LIST, value: index) -> LIST
             : extend(LISTa, LISTb) -> LISTa

function-ret : print_ret(value)
             : input()
             : input_int()
             : input_char()
             : int(value)
             : float(value)
             : str(value)
             : list(value)
             : is_num(value)
             : is_str(value)
             : is_list(value)
             : is_func(value)
             : len(STRING|LIST)

statements   : NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

statement		 : KEYWORD:return expr?
						 : KEYWORD:continue
						 : KEYWORD:break
						 : expr

expr         : KEYWORD:var IDENTIFIER EQ expr
             : comp-expr ((KEYWORD:and|KEYWORD:or comp-expr))*

comp-expr    : not comp-expr
             : arith-expr ((EE|NE|LT|GT|LTE|GTE) arith-expr)*
             : string ((EE|NE)) string

arith-expr   : term ((PLUS|MINUS) term)*

term         : factor ((MUL|DIV) factor)*

factor       : (PLUS|MINUS) factor
             : power

power        : call (POW factor)*

call         : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom         : INT|FLOAT|STRING|LIST|IDENTIFIER
             : LPAREN expr RPAREN
             : list-expr
             : if-expr
             : for-expr
             : while-expr
             : func-def

list-expr    : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr      : KEYWORD:if expr KEYWORD:then
               (statement if-expr-b|if-expr-c?)
             | (NEWLINE statements KEYWORD:end|if-expr-b|if-expr-c)

if-expr-b    : KEYWORD:elif expr KEYWORD:then
               (statement if-expr-b|if-expr-c?)
             | (NEWLINE statements KEYWORD:end|if-expr-b|if-expr-c)

if-expr-c    : KEYWORD:else
               statement
             | (NEWLINE statements KEYWORD:end)

for-expr     : KEYWORD:for IDENTIFIER EQ expr KEYWORD:to expr (KEYWORD:step expr)? KEYWORD:then
               statement
             | (NEWLINE statements KEYWORD:end)

while-expr   : KEYWORD:while expr KEYWORD:then
               statement
             | (NEWLINE statements KEYWORD:end)

func-def     : KEYWORD:func IDENTIFIER?
               LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
               (ARROW expr)
             | (NEWLINE statements KEYWORD:end)
